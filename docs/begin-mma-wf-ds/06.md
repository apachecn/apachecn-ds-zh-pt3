# 六、统计数据分析

在本章中，我们将回顾使用 Wolfram 语言进行分析的概念和技术，以及通过方程进行线性校正和实现用于相同目的的 Wolfram 语言的特殊功能。使用统计函数。Wolfram 语言是统计和概率的有用工具。Mathematica 具有对描述性统计和随机分布、随机数和随机抽样方法进行数值和近似计算的功能，我们将在本节中看到。

## 随机数

在本节中，我们将回顾生成随机数的基本命令——对于整数、实数和复数。除了如何使随机数的结果可重现之外，我们还将看到执行带替换和不带替换的随机采样的函数。

要创建随机数，有几个函数可以生成随机整数和实数。RandomInteger 函数将生成输入的随机数；如果函数中没有输入参数，则生成间隔为 0 或 1。

```py
In[1]:= RandomInteger[]
Out[1]= 0

```

要输入一个范围，必须在函数内定义；例如，介于-1 和 1 之间。

```py
In[2]:= RandomInteger[{-1,1}]
Out[2]= 1

```

要生成一个随机数列表，我们需要定义列表中需要多少个数字。

```py
In[3]:= RandomInteger[{-1,1},7]
Out[3]= {-1,0,1,-1,-1,-1,1}

```

要重复这些数字，作为第二个参数添加列表或嵌套列表的形式。例如，创建一个包含七个项目的嵌套列表，其中每个子列表包含四个项目。

```py
In[4]:= RandomInteger[{-10,10},{7,4}]
Out[4]= {{-9,-2,6,10},{5,10,3,-7},{7,-2,-4,10},{-1,-2,8,-6},{-10,9,3,0},{-4,-9,-2,5},{3,1,10,-5}}

```

生成带小数点的随机数的函数称为 RandomReal。它的工作原理类似于 RandomInteger，其中区间写在花括号之间。

```py
In[5]:= RandomReal[]
Out[5]= 0.946141

```

还有一个针对复杂随机数和素数的命令。

```py
In[6]:= RandomComplex[]
Out[6]= 0.411636 +0.79253 I

```

对于随机质数，我们必须定义一个最小和最大区间——例如，如果它是前 100 的质数。

```py
In[7]:= RandomPrime[{1,100},6]
Out[7]= {43,11,61,83,61,79}

```

这种类型的函数生成伪随机数，以便您可以设置一个种子来生成数字。这是通过 SeedRandom 完成的。有了种子，我们可以确保生成的随机数的起始序列是相同的，以使随机输出可重复。要设置种子，请使用 SeedRandom 命令。在下面的例子中，我们将设置一个种子，后面跟着一个随机数序列；一旦种子被引入，结果应该是相同的。

```py
In[8]:= SeedRandom[6467789];RandomInteger[{-1,1},3]
Out[8]= {0,1,-1}

```

种子必须放在同一个代码块中才能生成结果。有选择方法的选项，如下例所示，我们选择了 MersenneTwister 方法，这是一种常用于生成随机数的方法。使用另一种方法使我们有可能产生不同的随机数序列。

```py
In[9]:= SeedRandom[Method→"MersenneTwister"];RandomInteger[{-1,1},{3,3}]//MatrixForm
Out[9]//MatrixForm=

```

![$$ \left(\begin{array}{ccc}1&amp; -1&amp; 1\\ {}1&amp; 0&amp; 0\\ {}0&amp; -1&amp; -1\end{array}\right) $$](img/500903_1_En_6_Chapter_TeX_Equa.png)T2】

为了返回原始值，种子不带参数地进入函数。

```py
In[10]:= SeedRandom[]

```

除了引入种子，我们还可以创建随机数块，其中的函数可以在本地使用，并且不会影响这些块之外的随机行为。这是通过 BlockRandom 函数完成的。

```py
In[11]:= BlockRandom[RandomReal[1]]
Out[11]= 0.943218

```

如果我们运行一个在 BlockRandom 中生成随机数的算法并声明我们自己的种子，这应该不会影响在 BlockRandom 之外生成随机数的其他进程。为了说明这一点，让我们看看这个例子。

```py
In[12]:=
SeedRandom[121];
{RandomReal[],BlockRandom[RandomReal[]],RandomReal[],RandomReal[]}
Out[12]= {0.0908251,0.194288,0.194288,0.296762}

```

正如所看到的，后一个过程产生了不同的随机数

### 随意采样

要用替代品制作样本，要使用的函数是 RandomChoice。为了选择单个项目，我们只写列表。我们将设置一个种子来获得相同的结果。

```py
In[13]:= SeedRandom[12345];
RanData=RandomReal[{0,1},10]
Out[13]= {0.121246,0.329922,0.782753,0.430168,0.223586,0.463053,0.738017,0.707618,0.790911,0.105714}

```

我们已经生成了一个由 10 个随机数组成的列表，范围从 0 到 1，现在我们继续从这些数字中随机选择一项。

```py
In[14]:= RandomChoice[RanData]
Out[14]= 0.738017

```

这从 10 个项目的列表中给了我们一个结果。同样，我们可以用若干个元素来选择样本数，形式如下:RandomChoice["data "，" number of samples "，" number of elements"]。在这 10 个元素中，我们现在将挑选三个带有一个元素的样本。

```py
In[15]:= RandomChoice[RanData,{3,1}]
Out[15]= {{0.329922},{0.738017},{0.223586}}

```

虽然，如果我们希望它在同一个样本中，我们只需要指定要选择的元素的数量。

```py
In[16]:= RandomChoice[RanData,5]
Out[16]= {0.790911,0.463053,0.329922,0.430168,0.329922}

```

要获得无替换的采样，请使用 RandomSample。此函数不会多次从数据列表中选择列表项。为了进行选择，我们只指定样本中元素的数量作为第二个参数，因为第一个参数对应于数据列表。

```py
In[17]:= RandomSample[RanData,9]
Out[17]= {0.105714,0.790911,0.463053,0.738017,0.707618,0.782753,0.430168,0.223586,0.329922}

```

仔细观察，我们注意到没有重复的值。列表中的每一项在抽样中被选中的概率是相等的。

在列表中的每一项都有与之相关的特定权重的情况下，为了输入这些项，我们使用以下形式的表达式{w1，w2，w3...} → {元素 1，元素 2，元素 3...};项目列表与替换取样的特定重量相关联。

我们将标记权重列表，并通过关联权重和元素进行采样。

```py
In[18]:= W={0.03`,0.08`,0.22`,0.04`,0.12`,0.3`,0.12`,0.03`,0.04`,0.02`};
RandomChoice[W→RanData,2]
Out[18]= {0.223586,0.430168}

```

正如我们所注意到的，它们是根据每个元素的权重来选择的。对于没有替换的采样，过程是类似的。

```py
In[19]:= RandomSample[W→RanData,3]
Out[19]= {0.463053,0.738017,0.223586}

```

### 系统抽样

要执行系统抽样，我们必须确定样本的大小，m。要获得样本大小，我们可以列出列表中的项目或获得列表的长度。首先，我们将创建一个包含 200 个素数的列表。

```py
In[20]:= SeedRandom[09876]
RPrime=RandomPrime[{1,100},200];
Length[RPrime]
Out[20]= 200

```

我们已经计算了样本量，所以必须确定具体样本的大小；在这种情况下，我们需要 20 个元素的样本。一旦样本被确定，我们将计算表示的采样 j 的区间；j 通过一个比率来计算，即原始样本大小除以指定样本中的元素总数。

```py
In[21]:= j=Length[RPrime]/20
Out[21]= 10

```

这意味着我们新样本的采样间隔将从 1 到 10。从这里，我们在区间内选择一个随机数，从那里加上 j 次选择下一个元素；也就是说，对于第一个元素，它将是范围为[1，10]的随机 h 数，对于第二个元素，它将是 h + j，对于第三个元素，它将是 h +3j，依此类推，直到它达到原始样本的大小。

我们选择了 1 到 10 之间的一个随机数。

```py
In[22]:= RandomSample[Range[10],1]
Out[22]= {9}

```

结果意味着我们从第九个元素中选择。我们部署列表是为了更好地查看数据。

```py
In[23]:= RPrime
Out[23]= {17,11,67,97,11,73,71,61,71,31,59,29,79,7,71,89,79,11,2,29,97,61,2,71,3,79,31,83,83,17,37,89,41,31,61,7,11,53,17,61,71,2,53,23,29,59,11,41,13,71,3,53,13,61,19,2,17,17,59,3,11,41,83,59,41,47,13,59,17,5,5,59,79,37,97,7,11,23,41,83,67,79,73,73,73,41,79,17,59,37,83,71,73,17,2,11,41,89,97,7,2,23,13,67,79,83,5,61,47,73,61,97,53,53,2,89,19,19,61,89,83,43,73,3,83,17,5,89,29,23,7,23,53,97,2,83,13,17,37,2,19,59,79,29,43,19,7,43,59,47,3,41,23,53,37,59,29,83,37,59,19,59,31,89,2,67,47,47,97,2,47,97,41,11,43,37,7,59,67,83,89,2,17,13,2,7,73,83,89,2,3,59,17,19,73,13,53,29,89,83}

```

要获得要选择的项目的位置，它将是选择的随机数，即 9 加上 n 乘以 j，直到有 20 个元素。

```py
In[24]:= Table[9+n*j,{n,0,19}]
Out[24]= {9,19,29,39,49,59,69,79,89,99,109,119,129,139,149,159,169,179,189,199}

```

Note

记住位置索引从 1 到 n 个元素开始。

我们必须选择前面输出中显示的位置。为了进行选择，我们将使用双方括号符号。

```py
In[25]:= Table[RPrime[[9+n*j]],{n,0,19}]
Out[25]= {71,2,83,17,13,59,17,41,59,97,47,61,29,37,59,37,97,67,89,89}

```

让我们更好地看看所选的元素，在 MapAt 和 Style 的帮助下用红色突出显示它们。

```py
In[26]:= MapAt[Style[#,FontColor→ColorData["HTML"]["Red"]]&,RPrime,{#}&/@ {9,19,29,39,49,59,69,79,89,99,109,119,129,139,149,159,169,179,189,199}
Out[26]= {17,11,67,97,11,73,71,61,71,31,59,29,79,7,71,89,79,11,2,29,97,61,2,71,3,79,31,83,83,17,37,89,41,31,61,7,11,53,17,61,71,2,53,23,29,59,11,41,13,71,3,53,13,61,19,2,17,17,59,3,11,41,83,59,41,47,13,59,17,5,5,59,79,37,97,7,11,23,41,83,67,79,73,73,73,41,79,17,59,37,83,71,73,17,2,11,41,89,97,7,2,23,13,67,79,83,5,61,47,73,61,97,53,53,2,89,19,19,61,89,83,43,73,3,83,17,5,89,29,23,7,23,53,97,2,83,13,17,37,2,19,59,79,29,43,19,7,43,59,47,3,41,23,53,37,59,29,83,37,59,19,59,31,89,2,67,47,47,97,2,47,97,41,11,43,37,7,59,67,83,89,2,17,13,2,7,73,83,89,2,3,59,17,19,73,13,53,29,89,83}

```

正如我们所看到的，系统抽样并不能产生完全随机的样本。当我们选择第一个项目来创建新样本时，随机选择出现在第一部分。一旦选择了第一个项目，其他选择将从一系列非随机数中产生。另一个要考虑的方面是原始样本的顺序；如果元素之间存在周期性，这可能导致元素选择的很大可变性。

## 常用统计方法

掌握常用的统计公式对于理解数据在一组条件下的表现至关重要。描述性统计是在收集数据后实施的，是探索性数据分析过程中的第一步，它使您能够从发现模式、异常、趋势、季节性、变化等方面发现所收集数据的洞察力。

探索性数据分析被描述为一套分析技术，其目的是检测初看不到或一旦收集到数据就显现出来的特征。这项技术的基本结构依赖于数字数据分析、图形表示和统计模型。使用数据探索性分析的许多原因包括检查缺失的数据、描述基础结构的一般和特殊想法、分析与模型创建相关的不同假设等等。

Jhon Tukey 在 1977 年提出了这种方法。要更深入地了解这一技术，请访问以下参考资料，*探索性数据分析*(图基，J. W. [1977]，第 2 卷，第 131-160 页)。

### 集中趋势测量

给定一个数据样本，我们可以计算描述性的措施。集中趋势测量是那些给我们提供关于要研究的数据的平均值的信息的参数。

平均值，也称为算术平均值，是根据样本值的总和除以元素数量的总和计算的参数。Mean 函数计算平均值。

```py
In[27]:= List1=Table[Prime[i],{i,10}];
"Prime list :" <>ToString@ List1
"Mean: "<>ToString@Mean@N@List1
Out[27]= Prime list :{2, 3, 5, 7, 11, 13, 17, 19, 23, 29}
Out[27]= Mean: 12.9

```

Note

符号<>是 StringJoin 的缩写。

中位数是将样本分成两个相等部分的值，因为它是数据的中点，所以中位数是相对于数据数量的对称值。中值函数给出了这个值。

```py
In[28]:= "Median: "<>ToString@Median@List1
Out[28]= Median: 12

```

众数是样本最常见的值。我们使用 Counts 命令，它给出了列表中每一项出现的次数。

```py
In[29]:= Counts[List1]
Out[29]= <|2→1,3→1,5→1,7→1,11→1,13→1,17→1,19→1,23→1,29→1|>

```

在这种情况下，出现次数为 1。没有重复的值；我们可以说，在这个数据样本中没有模式。

### 离差的度量

离差测量揭示了样品中存在的可变性的信息。范围告诉我们数据变化的间隔。这是通过减去最大值和最小值得到的。Max 和 Min 函数返回列表的最大值和最小值。

```py
In[30]:= "Range: "<>ToString[Max[List1]-Min[List1]]
Out[30]= Range: 27

```

方差是通过减去样本中每个元素的平均值获得的度量。结果被平方，然后将它们相加。总和除以样本的大小。它的功能是方差。

```py
In[31]:= "Variance: "<>ToString[N[Variance[List1],3]]
Out[31]= Variance: 81.4

```

标准差是从方差的平方根或通过标准差函数获得的度量。

```py
In[32]:= {"Square root of Variance: " <>ToString[N[Sqrt[Variance[List1]],2]],
"StandardDeviation: " <>ToString[N[StandardDeviation[List1],2]]}
Out[32]= {Square root of Variance: 9.0,StandardDeviation: 9.0}

```

标准分数是一个称为 z 的分数，衡量样本中每个元素的算术平均值与标准偏差的差距。数学方程式是![$$ z=\frac{\mathrm{x}-\mu }{\sigma } $$](img/500903_1_En_6_Chapter_TeX_IEq1.png) **，**其中 x 是测量值，μ是平均值，σ是标准差。如果 z 为正，则意味着该元素大于平均值。当 z 为负时，情况正好相反。我们将确定列表中第二项的 z 值。

```py
In[33]:= z=N[(List1[[2]]-Mean@List1)/StandardDeviation@List1,3];
"z score: " <> ToString@z
Out[33]= z score: -1.10

```

这意味着第二个要素的得分低于平均水平 1.10 倍。

四分位数计算将数据分成四等份。下四分位数对应于数据的 25%四分位数，而第二个四分位数是 50%，第三个四分位数(上四分位数)是 75%，第四个四分位数是 100%。为了计算四分位数，我们使用 quartiles 函数，它依次给出第一、第二和第三个四分位数的值。

```py
In[34]:= "Quartiles: " <> ToString@Quartiles[List1]
Out[34]= Quartiles: {5, 12, 19}

```

如果我们想得到单个值，我们使用分位数函数，后面跟着要计算的百分位数。然后，对于第三个四分位数(第 75 个百分位数)的计算，我们使用以下公式。

```py
In[35]:= Quantile[List1,0.75]
Out[35]= 19

```

要计算四分位距，即上下四分位之间的差值，函数为 interquartile range。

```py
In[36]:= InterquartileRange[List1]
Out[36]= 14

```

## 统计图表

### 条形图

有时，当我们进行统计研究时，有可能发现定量和定性变量；对于这些变量，我们可以创建一个条形图表示。条形图(图 [6-1](#Fig1) )是一种图形表示，离散定性变量的频率数显示在轴上。

![img/500903_1_En_6_Fig1_HTML.jpg](img/500903_1_En_6_Fig1_HTML.jpg)

图 6-1

条形图

```py
In[37]:= BarChart[{1,2,3,4},ChartLabels→{"feature 1","feature 2","feature 3","feature 4"}]
Out[37]=

```

定性变量的不同模态位于其中一个轴上。另一个轴显示给定范围内每个类别的值或频率。特征 2 条具有相关联的值 2。图表的方向可以是垂直的，其中类别位于水平轴上，条形是垂直的；也可以是水平的，其中类别位于垂直轴上，条形是水平的(图 [6-2](#Fig2) )。

![img/500903_1_En_6_Fig2_HTML.jpg](img/500903_1_En_6_Fig2_HTML.jpg)

图 6-2

底部和左侧原点条形图

```py
In[38]:= GraphicsRow[{BarChart[{1,2,3,4},ChartLabels→{"feature 1","feature 2","feature 3","feature 4"},BarOrigin→Bottom,ChartStyle→LightBlue],BarChart[{1,2,3,4},ChartLabels→{"feature 1","feature 2","feature 3","feature 4"},BarOrigin→Left,ChartStyle→LightRed]}]
Out[38]=

```

条形图可用于比较不同类别的量值，并观察值如何根据固定变量(例如，每个要素)而变化。此外，我们可以选择如何显示条形图:简单地说，我们显示一个序列，如前面的例子所示；分组数据，包含多个数据系列，由不同类型的条形图表示；或堆叠式，其中条形图被分成多个部分，用不同的颜色表示不同的类别。百分比布局以百分比形式显示，如图 [6-3](#Fig3) 所示。

```py
In[39]:= Labeled[GraphicsGrid[
{{
BarChart[{{4,3,2,1},{1,2,3},{3,5}},ChartLayout→"Grouped",ColorFunction→"SolarColors"],
BarChart[{1,2,3,4},ChartStyle→LightRed,ChartLayout→"Stepped"]},
{BarChart[{{4,3,2,1},{1,2,3},{6,5}},ChartLayout→"Stacked"],
BarChart[{{4,3,2,1},{1,2,3},{6,5}},ChartLayout→"Percentile",ColorFunction→"DarkRainbow"]
}},Frame→All,FrameStyle→Directive[Black,Dashed],Background→LightBlue,ImageSize→500],"Bar Charts",Top]
Out[39]=

```

![img/500903_1_En_6_Fig3_HTML.jpg](img/500903_1_En_6_Fig3_HTML.jpg)

图 6-3

条形图网格

还有 3D 图形的对应物，有 BarChart3D(图 [6-4](#Fig4) )。

![img/500903_1_En_6_Fig4_HTML.jpg](img/500903_1_En_6_Fig4_HTML.jpg)

图 6-4

三维条形图网格

```py
In[40]:= SeedRandom[123]
Labeled[GraphicsGrid[
{{
BarChart3D[{{4,3,2,1},{1,2,3},{3,5}},ChartLayout→"Grouped",ColorFunction→"SolarColors"],
BarChart3D[{1,2,3,4},ChartStyle→LightRed,ChartLayout→"Stepped"]},
{BarChart3D[RandomReal[1,{10,5}],ChartLayout→"Stacked"],
BarChart3D[{{4,3,2,1},{1,2,3},{6,5}},ChartLayout→"Percentile",ColorFunction→"DarkRainbow"]
}},Frame→All,FrameStyle→Directive[Red,Thick],Background→LightBlue,ImageSize→500],"3D Bar Charts",Top,Frame→True,Background→White]
Out[40]=

```

### 直方图

直方图是统计研究中常用的一种可视化方式。通过直方图，我们可以看到样本是如何分布的。直方图用于表示定量变量的频率。变量的类别位于水平轴上，频率位于另一个轴上。在接下来的示例中，我们将从 0 到 1 之间的 50 个随机值的总体绘制直方图，并将箱数设置为 10。直方图的第二个参数是定义面元的数量(图 [6-5](#Fig5) )。

![img/500903_1_En_6_Fig5_HTML.jpg](img/500903_1_En_6_Fig5_HTML.jpg)

图 6-5

随机实数直方图

```py
In[41]:= SeedRandom[4322]
hist1=Table[RandomReal[{2,3}],{i,0,20}];
Histogram[hist1,10]
Out[41]=

```

Note

处理图表时，如果您将指针光标放在图形上，信息提示将显示标记值。

就像条形图一样，有多种方法可以编辑直方图的原点以及直方图的显示方式——堆叠或重叠——如图 [6-6](#Fig6) 所示。

![img/500903_1_En_6_Fig6_HTML.jpg](img/500903_1_En_6_Fig6_HTML.jpg)

图 6-6

直方图形状网格

```py
In[42]:=
hist2=Table[Cos[i],{i,1,20}];
hist3=Table[Sin[i],{i,1,10}];
GraphicsColumn[{Histogram[{hist1,hist2},10,BarOrigin→Left,ChartStyle→"Pastel",ChartLegends→{"rand num","Cos(x)"}],Histogram[{hist2,hist3},10,ChartLayout→"Overlapped",ChartStyle→"Pastel",ChartLegends→{"Cos(x)","Sin(x)"}],Histogram[{hist2,hist3},10,ChartLayout→"Stacked",ChartStyle→"Pastel",ChartLegends→{"Cos(x)","Sin(x)"}]}]
Out[42]=

```

考虑到这一点，我们也可以使用成对直方图绘制双向直方图。这些可以是水平或垂直方向，并包含两个数据系列，其条走向相反(图 [6-7](#Fig7) )。

![img/500903_1_En_6_Fig7_HTML.jpg](img/500903_1_En_6_Fig7_HTML.jpg)

图 6-7

不同原点的成对直方图

```py
In[43]:= SeedRandom[123]
GraphicsRow[{PairedHistogram[{RandomReal[{0,1},20]},{RandomReal[{0,1},20]},BarOrigin→Left],PairedHistogram[{RandomReal[{0,1},20]},{RandomReal[{0,1},20]},10,BarOrigin→Top,ChartStyle→"Pastel"]}]
Out[43]=

```

### 饼图和扇形图

饼图是分成两个或更多部分的圆形。它们用于表示数量变量，这些变量共同构成一个总数；例如，扇区的大小与它所代表的价值成比例，并用百分比表示，这只能提供相对的定量信息。饼状图是用饼状图命令制作的(图 [6-8](#Fig8) )。

![img/500903_1_En_6_Fig8_HTML.jpg](img/500903_1_En_6_Fig8_HTML.jpg)

图 6-8

饼图

```py
In[183]:= GraphicsRow[{PieChart[{1,1,1},ChartLegends→{"part a","part b","part c"},ChartStyle→{LightRed, LightBlue, LightYellow}],
PieChart[{1,1},ChartLegends→{"part a","part b"},ChartStyle→"SunsetColors"]}]
Out[183]=

```

使用扇形图表命令绘制扇形图表(图 [6-9](#Fig9) )。它们用于比较发生在同一地点的不同数据。它们是由 x 的大小与 y 的半径值成比例构成的。对于所有线段，表示数量的维度必须相同。

![img/500903_1_En_6_Fig9_HTML.jpg](img/500903_1_En_6_Fig9_HTML.jpg)

图 6-9

扇形图表

```py
In[45]:= SectorChart[{{2,1},{1,2}},ChartLegends→{"Sector a","Sector b"},ChartStyle→{LightRed, LightYellow}]
Out[45]=

```

对于看到的每个图形，都有一个相应的命令来创建三维图形；这些如图 [6-10](#Fig10) 所示。

![img/500903_1_En_6_Fig10_HTML.jpg](img/500903_1_En_6_Fig10_HTML.jpg)

图 6-10

三维网格图

```py
In[46]:= GraphicsGrid[
{{SectorChart3D[{{2,1,1},{3,1,2},{1,2,2}},PlotLabel→"3D Sector chart",ChartStyle→{Red, Blue,Yellow}],
PieChart3D[{1,1,1},ChartStyle→"GrayTones",PlotLabel→"3D Pie Chart"]},
{Histogram3D[Table[{i^3,i^-1},{i,20}],10,ChartElementFunction→"GradientScaleCube",PlotLabel→"3D Histogram"],None}
},ImageSize→500,Frame→True,FrameStyle→Directive[Thick,Dotted]]
Out[46]=

```

### 箱线图

箱线图是一种表示和观察数据分布的方式。基本上，它用于突出显示一个或多个系列中数据分布的各个方面。为了绘制一个箱线图，我们使用 BoxWhiskerChart 命令(图 [6-11](#Fig11) )。

![img/500903_1_En_6_Fig11_HTML.jpg](img/500903_1_En_6_Fig11_HTML.jpg)

图 6-11

箱形图

```py
In[47]:= SeedRandom[1234]
BoxWhiskerChart[{Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,15}]},ChartLabels→{"Chart 1","Chart 2","Chart 3"}]
Out[47]=

```

该方框由标记分布的四分位数范围的矩形表示。从下到上的第一条线标记第一个四分位数的值(25%)，穿过方框的线是中间值，划定方框的最后一条线是第三个四分位数(75%)。胡须是标记最大值和最小值的线。当鼠标光标经过绘图时，将显示有关数据的信息；这包括最小值、最大值、中间值、第 75 个百分位数和第 1 个四分位数。根据我们使用的规范，这会影响显示哪些参数以及如何显示(图 [6-12](#Fig12) )。

![img/500903_1_En_6_Fig12_HTML.jpg](img/500903_1_En_6_Fig12_HTML.jpg)

图 6-12

多盒图

```py
In[48]:= SeedRandom[123]
GraphicsGrid[{{BoxWhiskerChart[{Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,15}]},#,PlotLabel→Style[#,White],
ImageSize→Medium,ChartStyle→"MintColors",FrameStyle→Directive[White,12]]&["Median"],
BoxWhiskerChart[{Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,15}]},#,PlotLabel→Style[#,LightOrange],ImageSize→Medium,ChartStyle→"MintColors",FrameStyle→Directive[Orange,12]]&["Basic"],
BoxWhiskerChart[{Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,15}]},#,PlotLabel→Style[#,White],ImageSize→Medium,ChartStyle→"MintColors",FrameStyle→Directive[White,12]]}&["Notched"],
{BoxWhiskerChart[{Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,15}]},#,PlotLabel→Style[#,LightOrange],ImageSize→Medium,ChartStyle→"MintColors",FrameStyle→Directive[Orange,12]]&["Outliers"],
BoxWhiskerChart[{Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,15}]},#,PlotLabel→Style[#,White],ImageSize→Medium,ChartStyle→"MintColors",FrameStyle→Directive[White,12]]&["Mean"],
BoxWhiskerChart[{Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,50}],Table[RandomReal[],{i,0,15}]},#,PlotLabel→Style[#,LightOrange],ImageSize→Medium,ChartStyle→"MintColors",FrameStyle→Directive[Orange,12]]&["Diamond"]}},FrameTicksStyle→18,Frame→{None,None,{{1,1}→True,{2,2}→True,{1,3}→True}},FrameStyle→Directive[Thick,Red],Background→Black]
Out[48]=

```

中值是默认规格；它显示了盒子中心的中间值。基本是只显示盒子。凹口显示中位数的置信区间。异常值显示并标记非典型点。平均值表示分布的平均值，菱形表示平均值的置信区间。

### 分布图

小提琴图用于可视化数据和概率密度的分布。要绘制小提琴图(图 [6-13](#Fig13) ，使用 DistributionChart 命令。

```py
In[49]:= DistributionChart[Table[i^Exp[i],{i,0,1,0.01}]]
Out[49]=

```

![img/500903_1_En_6_Fig13_HTML.jpg](img/500903_1_En_6_Fig13_HTML.jpg)

图 6-13

小提琴情节

如图所示，该图是盒须图和密度图的组合，显示了数据的分布情况。分布图有不同的图形形状(图 [6-14](#Fig14) )。

![img/500903_1_En_6_Fig14_HTML.jpg](img/500903_1_En_6_Fig14_HTML.jpg)

图 6-14

不同形状的小提琴曲

```py
In[50]:= GraphicsGrid[{{DistributionChart[Table[i^Exp[i],{i,0,2,0.1}],ChartElementFunction→"SmoothDensity",PlotLabel→"SmoothDensity"],DistributionChart[Table[i^Exp[i],{i,1,2,0.1}],ChartElementFunction→"Density",PlotLabel→"Density",FrameStyle→Directive[Red,12]]},{DistributionChart[Table[i^Exp[i],{i,0,1,0.09}],ChartElementFunction→"HistogramDensity",PlotLabel→"HistogramDensity",FrameStyle→Directive[Red,12]],DistributionChart[Table[i^Exp[i],{i,0,1,0.0112}],ChartElementFunction→"PointDensity",PlotLabel→"PointDensity"]}},ImageSize→Medium,FrameStyle→Directive[Thickness[0.02],LightGray],Dividers→{2→Directive[Black,Dotted],2→Directive[Black,Dotted]},Frame→{1→False,False}]
Out[50]=

```

### 图表调色板

向图表添加选项的另一种方法是通过图表元素方案调板，该调板位于调板菜单中(调板→图表元素方案)。该调色板如图 [6-15](#Fig15) 所示。

![img/500903_1_En_6_Fig15_HTML.jpg](img/500903_1_En_6_Fig15_HTML.jpg)

图 6-15

图表元素方案调色板

在调色板中，您会发现三个类别。图表类型是我们选择图表类型的地方。这包含四个选项卡:(1)常规，其中的图形是从条形图，部门，页脚，和其他；(2)与数据分布相关的统计图；(3)财务，与财务数据图表相关联；以及(4)计量器，其是测量的图表。第二类是用 ChartElemenFunction 选项选择图形的形状。第三个类别用于预览从前面的类别中选择的选项。

为了说明这一点，让我们看看下面的练习。首先，我们将制作一个直方图的密度图，稍后我们将借助调色板修改图形的形状。为了绘制直方图的密度，我们使用 DensityHistogram 命令(图 [6-16](#Fig16) )。

![img/500903_1_En_6_Fig16_HTML.jpg](img/500903_1_En_6_Fig16_HTML.jpg)

图 6-16

密度直方图

```py
In[51]:= DensityHistogram[Flatten[Table[{x^2+y^2,x^2-y^2},{x,0,2,0.1},{y,0,2,0.1}],1],ChartBaseStyle→Red,ColorFunction→"SolarColors",Background→Black,FrameStyle→Directive[White,Thick],FrameLabel→{"X","Y"},ImageSize→300]
Out[51]=

```

图表完成后，我们将添加一个托盘头选项，并打开图表元素方案面板。在“图表类型”中，我们单击“统计”选项卡，然后选择“密度直方图”图表。选择图表后，我们现在转到图表元素，选择表格类型为气泡。然后，我们去选项预览，看看我们的图表会是什么样子；如果我们点击形状，会出现一个带有其他形状的弹出菜单；我们选择六边形。图 [6-17](#Fig17) 显示了所选图表元素的预览效果。

![img/500903_1_En_6_Fig17_HTML.jpg](img/500903_1_En_6_Fig17_HTML.jpg)

图 6-17

选择密度直方图选项

完成选择后，我们单击 insert 按钮，插入以下代码:ChartElementFunction→ChartElementDataFunction[" Bubble "，" Shape" → "Hexagon"]。为了正确绘制它，我们将此代码添加为一个选项，并继续绘制它(图 [6-18](#Fig18) )以观察添加的新选项。

```py
In[52]:= DensityHistogram[Flatten[Table[{x^2+y^2,x^2-y^2},{x,0,2,0.1},{y,0,2,0.1}],1],ChartBaseStyle→Red,ColorFunction→"SolarColors",Background→Black,FrameStyle→Directive[White,Thick],FrameLabel→{"X","Y"},ImageSize→300,ChartElementFunction→ChartElementDataFunction["Bubble","Shape"→"Hexagon"]]
Out[52]=

```

![img/500903_1_En_6_Fig18_HTML.jpg](img/500903_1_En_6_Fig18_HTML.jpg)

图 6-18

六边形密度直方图

DensityHistogram 命令允许您选择如何显示数据沿坐标轴的分布；如果我们选择方法类型作为选项，它可以是尺寸、箱线图或直方图(图 [6-19](#Fig19) )。

```py
In[53]:= Hist=Flatten[Table[{x^2+y^2,x^2-y^2},{x,0,2,0.1},{y,0,2,0.1}],1];
{MenuView[{DensityHistogram[Hist,Method→{"DistributionAxes"→True},ColorFunction→GrayLevel,ChartBaseStyle→Directive[FaceForm[Opacity[0.5]],EdgeForm[Red]],ChartLegends→BarLegend[Automatic,LegendMarkerSize→70],PlotLabel→Style[" Density Histogram 1", Bold],ChartElementFunction→#,ImageSize→200],
DensityHistogram[Hist,Method→{"DistributionAxes"→"Histogram"},ChartLegends→BarLegend[Automatic,LegendMarkerSize→70],PlotLabel→Style[" Density Histogram 2", Bold],ChartBaseStyle→EdgeForm[Thick],PlotTheme→"Scientific",ChartElementFunction→#,ImageSize→200],
DensityHistogram[Hist,Method→{"DistributionAxes"→"BoxWhisker"},ColorFunction→"BlueGreenYellow",PlotLabel→Style[" Density Histogram 3", Bold],ChartLegends→BarLegend[Automatic,LegendMarkerSize→70],ChartElementFunction→#,ImageSize→200]
}]&[ChartElementDataFunction["Bubble","Shape"→"Hexagon"]],{
GraphicsRow[{
DensityHistogram[Hist,Method→{"DistributionAxes"→True},ColorFunction→GrayLevel,ChartBaseStyle→Directive[FaceForm[Opacity[0.5]],EdgeForm[Red]],ChartLegends→BarLegend[Automatic,LegendMarkerSize→70],PlotLabel→Style[" Density Histogram 1", Bold],ChartElementFunction→#,ImageSize→130],
DensityHistogram[Hist,Method→{"DistributionAxes"→"Histogram"},ChartLegends→BarLegend[Automatic,LegendMarkerSize→70],PlotLabel→Style[" Density Histogram 2", Bold],ChartBaseStyle→EdgeForm[Thick],PlotTheme→"Scientific",ChartElementFunction→#,ImageSize→130],
DensityHistogram[Hist,Method→{"DistributionAxes"→"BoxWhisker"},ColorFunction→"BlueGreenYellow",PlotLabel→Style[" Density Histogram 3", Bold],ChartLegends→BarLegend[Automatic,LegendMarkerSize→70],ChartElementFunction→#,ImageSize→130]
}&[ChartElementDataFunction["Bubble","Shape"→"Hexagon"]]]}}
Out[53]=

```

![img/500903_1_En_6_Fig19_HTML.jpg](img/500903_1_En_6_Fig19_HTML.jpg)

图 6-19

三种不同方法图的菜单视图

图在里面显示为一个菜单，因此要访问不同的图形，您必须选择菜单中的每个图形。尽管如此，我们还是以小比例展示了这些图，以展示它们应该是什么样子(图 [6-19](#Fig19) )。第一张图显示了数据沿轴分布的维度。第二个以直方图的形式显示了数据的分布，第三个显示了箱线图。

## 普通最小二乘法

普通最小二乘法基本上包括找到最适合数据的线。这种方法用于研究因变量和自变量之间的关系。该方法基于寻找 y = mx + b 形式的线的表达式，其中 x 是自变量，y 是因变量，m 是斜率，b 是 y 截距。斜率和排序到原点 b 的计算由以下等式获得。

![$$ {\displaystyle \begin{array}{l}m=\frac{n\ast \sum \left(x\ast y\right)-\sum x\ast \sum y}{n\ast \sum {x}^2-{\left|\sum x\right|}^2}\\ {}b=\frac{\sum y\ast \sum {x}^2-\sum x\ast \sum \left(x\ast y\right)}{n\ast \sum {x}^2-{\left|\sum x\right|}^2}\end{array}} $$](img/500903_1_En_6_Chapter_TeX_Equb.png)T2】

总和用希腊文大写字母 sigma(≘)表示；n 是样本中的数据量。该方法针对测量的数据对和斜率值进行计算，并计算 y 轴截距源以创建数据到线的最佳拟合。通过代入一般方程，我们得到数据集的直线方程。

为了说明这种方法是什么样的，让我们看看下面的例子，用我们的观点来说明因变量和自变量。

<colgroup><col class="tcol1 align-left"> <col class="tcol2 align-left"> <col class="tcol3 align-left"> <col class="tcol4 align-left"> <col class="tcol5 align-left"> <col class="tcol6 align-left"> <col class="tcol7 align-left"> <col class="tcol8 align-left"> <col class="tcol9 align-left"></colgroup> 
| 

X

 | 

-1

 | 

Zero

 | 

one

 | 

Two

 | 

three

 | 

four

 | 

five

 | 

seven

 |
| --- | --- | --- | --- | --- | --- | --- | --- | --- |
| Y | Ten | nine | seven | five | four | three | Zero | -1 |

```py
In[54]:= Data={{-1,10},{0,9},{1,7},{2,5},{3,4},{4,3},{5,0},{7,-1}};
Grid[Transpose[Prepend[Data,{"X","Y"}]],Dividers→{2→True,2→True},Alignment→Center]
Out[54]=

```

现在，我们必须计算获得斜率和 y 轴截距所需的数据。

```py
In[55]:=
n=Length[Data];
SumX=Total@Data[[All,1]];
SumY=Total@Data[[All,2]];
SumXY=Total[Data[[All,1]]*Data[[All,2]]];
SumXSqre=Total@(Data[[All,1]]^2);

```

![$$ {\displaystyle \begin{array}{l}m=N@\frac{n\ast \mathrm{SumXY}-\mathrm{SumX}\ast \mathrm{SumY}}{n\ast \mathrm{SumXSqre}-\mathrm{Abs}{\left[\mathrm{SumX}\right]}^2};\\ {}b=N@\frac{\mathrm{SumY}\ast \mathrm{SumXSqre}-\mathrm{SumX}\ast \mathrm{SumXY}}{n\ast \mathrm{SumXSqre}-\mathrm{Abs}{\left[\mathrm{SumX}\right]}^2};\end{array}} $$](img/500903_1_En_6_Chapter_TeX_Equc.png)T2】

为了求解形状 y = mx + b 的方程，我们使用 solve 命令。第一个参数是方程，第二个参数是变量要求解的。要进入等式，我们必须使用相同的双符号，因为单个等号表示 set 指令。

```py
In[56]:= Solve[SetPrecision[y==m*x+b,3],y]
Out[56]= {{y→8.47-1.47 x}}

```

这导致直线的等式为 y = 1.47 x + 8.47。根据这个等式，我们将绘制出这些点和最适合这些点的直线(图 [6-20](#Fig20) )。

![img/500903_1_En_6_Fig20_HTML.jpg](img/500903_1_En_6_Fig20_HTML.jpg)

图 6-20

数据图和拟合曲线

```py
In[57]:=
Show[Plot[b+m x,{x,-1,8},PlotLegends→Placed[" Linear Fit: y=-1.47 x + 8.47",{0.6,0.8}],PlotRange→Automatic],ListPlot[Data,PlotStyle→Red]]
Out[57]=

```

得到方程后，我们观察到这是一个具有负斜率的模型；这一点得到了蓝色方程式图表的证实。

### 皮尔逊系数

告诉我们两个点都符合方程的度量是皮尔逊相关系数 r。当发现点具有正斜率时，r 将具有正值。当点是负斜率时，r 将具有负值。系数值决定了设置的正确程度；该值的范围从-1 到 1。当 r 的值为 1 或-1 时，它告诉我们点被精确地调整到直线上。r 越接近-1 或 1，表明研究变量之间似乎存在线性关系。否则，当 r 等于 0 时，它告诉我们设置不正确，因此可以得出结论，没有明显的线性关系。

确定系数的方程式如下:

![$$ r=\frac{Cov\left(x,y\right)}{\sigma_x\;{\sigma}_y}, $$](img/500903_1_En_6_Chapter_TeX_Equd.png)

其中 Cov 代表 x，y 的协方差。符号𝜎 <sub>*x*</sub> 、𝜎 <sub>*y*</sub> 代表 x 和 y 的标准差

现在，我们继续为创建的调整计算系数 r。为此，为了计算协方差和标准差，我们必须只引入 x 和 y 的点。

`In[58]:=` ![$$ r=N@\frac{\mathrm{Covariance}@@\left\{\mathrm{Data}\left[\left[\mathrm{All},1\right]\right],\mathrm{Data}\left[\left[\mathrm{All},2\right]\right]\right\}}{\mathrm{StandardDeviation}@\mathrm{Data}\left[\left[\mathrm{All},1\right]\right]\ast \mathrm{StandardDeviation}@\mathrm{Data}\left[\left[\mathrm{All},2\right]\right]} $$](img/500903_1_En_6_Chapter_TeX_IEq2.png)

`Out[58]= -0.987814`

给我们的结果接近 1；因此，我们可以说直线对数据来说是足够公平的。虽然可以通过方程来计算，但是 Mathematica 有一个函数可以进行这种计算。Correlation 从两个列表中计算系数，所以我们只需要在一个列表中输入 x 数据，在另一个列表中输入 y 数据。

```py
In[59]:= N@Correlation[Data[[All,1]],Data[[All,2]]]
Out[59]= -0.987814

```

我们得到了和上一个一样的结果。

### 线性拟合

即使使用前面提到的过程，Mathematica 也有专门的函数，通过使用 LinearModelFit 找到最佳的线性模型。给定数据集，我们编写 LinearModelFit 命令，其中包含要处理的数据和用于编写方程的变量。此外，我们可以使用工作精度来指定调整的精度级别。

```py
In[60]:= Model=LinearModelFit[Data,x,x,WorkingPrecision→10]
Out[60]= FittedModel [8.473684211-1.466165414 x]

```

正如我们所看到的，同样的等式返回给我们，但是更精确。在模型中，我们可以访问与数据、模型和其他调整参数相关的不同属性，以及拟合优度的度量等。为了说明这一点，我们来看看如何对 BestFit、BestFitParameters 和 Function 选项进行操作，它们分别以列表、最佳参数和纯函数的模型构造的形式返回最佳拟合的方程。

一个非常重要的方面是，试图使用拟合的方程(8.47 - 1.47 x)对未来值进行预测时，x 值超出范围可能会产生异常值，因为我们尚未真正确定 x 范围之外的方程关系是否实际满足。图 [6-21](#Fig21) 显示了拟合曲线的计算。

![img/500903_1_En_6_Fig21_HTML.jpg](img/500903_1_En_6_Fig21_HTML.jpg)

图 6-21

拟合参数、方程和皮尔逊系数

```py
In[61]:= {"\n"Framed["Best Fit Parameters b and m: "<>ToString[Model["BestFitParameters" ]],Background→LightYellow],"\n"Framed["Equation: "<>ToString[Model["BestFit" ]],Background→LightYellow],
"\n"Framed["Pure Function:"<>ToString[SetPrecision[Model["Function"],3]],Background→LightYellow],"\n"Framed["r coeficcient:"<>ToString[r],Background→LightYellow]}
Out[61]=

```

既然我们有了最佳拟合的直线，我们应该考虑 x 和 y 之间是否真的存在关系。您如何知道所做的调整是否充分描述了变量 x 和 y 之间的线性关系？为了解决这个问题，就有了残差的概念。

### 模型属性

残差可用于测量线与研究点的拟合程度。残差是正的或负的垂直偏差。残差点是因变量的观察值和预测调整值之间的差值。为了获得残差点，我们在模型中编写了 FitResiduals 属性。

```py
In[62]:= Model["FitResiduals"]
Out[62]= {0.06015038,0.52631579,-0.00751880,-0.54135338,-0.07518797,0.39097744,-1.14285714,0.78947368}

```

有了这些点，我们可以得到残差图(图 [6-22](#Fig22) )，这是变量 x 对残差点。

![img/500903_1_En_6_Fig22_HTML.jpg](img/500903_1_En_6_Fig22_HTML.jpg)

图 6-22

拟合数据的残差图

```py
In[63]:= ListPlot[Model["FitResiduals"],PlotStyle→{Red,Thick},PlotLabel→"Residual Plot",AxesLabel→{Style["X",Bold],Style["residual points",Bold]},Filling→Axis]
Out[63]=

```

若要仅显示单个预测的观察值和预测值，请使用 SinglePredictionConfidenceIntervalTable 选项。

```py
In[64]:= Model["SinglePredictionConfidenceIntervalTable"]
Out[64]=
Observed       Predicted        Standard Error    Confidence Interval
10             9.93984962       0.78481739        {8.0194706,11.8602286}
9              8.47368421       0.74856412        {6.6420138,10.3053546}
7              7.00751880       0.72287410        {5.2387096,8.7763280}
5              5.54135338       0.70889670        {3.8067456,7.2759611}
4              4.07518797       0.70732661        {2.3444221,5.8059538}
3              2.60902256       0.71824519        {0.8515399,4.3665052}
0              1.14285714       0.74110068        {-0.6705509,2.9562652}
-1             -1.78947368      0.81811053        {-3.7913180,0.2123707}

```

除了残差点，我们还可以从用 ParameterTable 属性调整的模型的参数中提取表格。

```py
In[65]:= Model["ParameterTable"]
Out[65]=
    |  Estimate       Standard Error     t-Statistic     P-Value
1   |  8.473684211    0.34167121         24.800697       2.8278226*10^-7
-x  | -1.466165414    0.094310214       -15.5461996      4.4832546*10^-6

```

系数如表所示。第一个系数是原点的纵坐标，与变量 e 相关的系数是斜率。这两个系数有各自的标准误差。为了知道参数的置信区间，我们编写属性 parametercconfidenceintervaltable。

```py
In[66]:= Model["ParameterConfidenceIntervalTable"]
Out[66]=
    |  Estimate       Standard Error     Confidence Interval  
1   |  8.473684211    0.34167121         {7.63764488,9.30972355} 
x   | -1.466165414    0.094310214        {-1.69693419,-.23539663}

```

默认的置信区间是 95%。有了这些置信值，我们可以绘制出该范围内或范围外的点(图 [6-23](#Fig23) )，从预测中提取值，并将置信区间的选项设置为 0.95。

![img/500903_1_En_6_Fig23_HTML.jpg](img/500903_1_En_6_Fig23_HTML.jpg)

图 6-23

填充区域表示 95%的置信区间。

```py
In[67]:= Model[x];
Model["SinglePredictionBands",ConfidenceLevel→0.95];
Show[ListPlot[Data,PlotStyle→Red],
Plot[{Model[x],Model["SinglePredictionBands",ConfidenceLevel→0.95]},{x,-1,10},Filling→{2→{1}}],PlotRange→{Automatic,{-1,10}},Frame→True,ImageSize→400]
Out[67]=

```

最后，为了获得与误差平方和相关的性质，我们使用变量的性质。

```py
In[68]:= Model["ANOVATable"]
Out[68]=
       | DF    SS             MS            F-Statistic    P-Value
x      | 1     107.213346     107.213346    241.68432      4.48325*10^-6
Error  | 6     2.6616541      0.44360902   
Total  | 7     109.8750000      

```